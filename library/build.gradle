apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'
apply from: "$rootDir/version.gradle"

def getExtProperty(prop, defaultValue) {
    return rootProject.ext.has(prop) ? rootProject.ext.get(prop) : defaultValue
}

android {

    compileSdkVersion getExtProperty('compileSdkVersion', 29)
    buildToolsVersion getExtProperty('buildToolsVersion', '29.0.0')

    defaultConfig {

        versionCode verison.code()
        versionName verison.name()

        minSdkVersion getExtProperty('minSdkVersion', 14)
        targetSdkVersion getExtProperty('targetSdkVersion', 29)
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    // 这个是解决lint报错的代码
    lintOptions {
        // 即使有报错也不会停止打包了
        abortOnError false
        // 防止在发布的时候出现因MissingTranslation导致Build Failed!
        disable 'MissingTranslation'
        disable 'ExtraTranslation'
    }
}

Properties properties = new Properties()
properties.load(rootProject.file('local.properties').newDataInputStream())
def bintrayUserValue = properties.getProperty('bintrayUser')
def bintrayKeyValue = properties.getProperty('bintrayKey')

publish {
    userOrg = 'hsl5430' // bintray注册的用户名
    groupId = 'com.mutou'
    artifactId = 'BadgeTabLayout' // bintray创建的package
    publishVersion = verison.name()
    desc = 'Similar to Radiogroup + RadioButton'
    website = 'https://github.com/hsl5430/BadgeTabLayout'

    if (bintrayUserValue != null) {
        bintrayUser = bintrayUserValue
    }
    if (bintrayKeyValue != null) {
        bintrayKey = bintrayKeyValue
        dryRun = false
    }
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
}
